// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© bullbearpanda
// github.com/bullbearpanda
// twitter.com/bullbearpanda

//@version=5
indicator("Panda Levels", overlay = true, max_lines_count = 500)

//------------------------------------------------------------------------------
//------------------------------------------------------------------------ Input
//------------------------------------------------------------------------------

plotDaily = input.bool(true, "Plot Daily Open")
plotWeekly = input.bool(true, "Plot Weekly Open")

//------------------------------------------------------------------------------
//-------------------------------------------------------------------- Variables
//------------------------------------------------------------------------------

forex = "0800-1700:23456"           // America/New_York
america = "0930-1600:23456"         // America/New_York
europe = "0800-1630:23456"          // Europe/London
asia_morning = "0900-1130:23456"    // Asia/Tokyo (Morning Session)
asia_afternoon = "1230-1500:23456"  // Asia/Tokyo (Afternoon Session)

//------------------------------------------------------------------------------
//-------------------------------------------------------------------- Utilities
//------------------------------------------------------------------------------

timeinrange(res, sess, tz) => not na(time(res, sess, tz))

//------------------------------------------------------------------------------
//----------------------------------------------------------------- Calculations
//------------------------------------------------------------------------------

isNewDay = timeframe.change("D")
isNewWeek = timeframe.change("W")

session_forex = timeinrange(timeframe.period, forex, "America/New_York") and syminfo.type == "forex"
session_europe = timeinrange(timeframe.period, europe, "Europe/London")
session_america = timeinrange(timeframe.period, america, "America/New_York")
session_asia = timeinrange(timeframe.period, asia_morning, "Asia/Tokyo") or timeinrange(timeframe.period, asia_afternoon, "Asia/Tokyo")

//------------------------------------------------------------------------------
//------------------------------------------------------------------------ Plots
//------------------------------------------------------------------------------

var line line_daily = na
var line line_weekly = na

var line line_america = na
var line line_europe = na

if isNewWeek and plotWeekly
    if not na(line_weekly)
        line_weekly.set_extend(extend.none)
        line_weekly.set_x2(bar_index)
    line_weekly := line.new(bar_index, open, bar_index + 1, open, extend = extend.right, color = color.purple, width = 2)

if timeframe.isintraday
    if isNewDay and dayofweek != dayofweek.monday and plotDaily
        if not na(line_daily)
            line_daily.set_extend(extend.none)
            line_daily.set_x2(bar_index)
        line_daily := line.new(bar_index, open, bar_index + 1, open, extend = extend.right, color = color.aqua, width = 1)

    if session_america and not session_america[1]
        line_america := line.new(bar_index, open, bar_index + 1, open, color = color.lime, style = line.style_dashed, width = 1)
    else if ((not session_america and session_america[1]) or session_america) and not na(line_america)
        line_america.set_x2(bar_index)

    if session_europe and not session_europe[1]
        line_europe := line.new(bar_index, open, bar_index + 1, open, color = color.fuchsia, style = line.style_dashed, width = 1)
    else if ((not session_europe and session_europe[1]) or session_europe) and not na(line_europe)
        line_europe.set_x2(bar_index)
