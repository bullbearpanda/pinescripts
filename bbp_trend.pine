// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© bullbearpanda
// github.com/bullbearpanda
// twitter.com/bullbearpanda

//@version=5
indicator("Panda Trend", overlay = true)
import TradingView/ta/7

//------------------------------------------------------------------------------
//-------------------------------------------------------------------- Variables
//------------------------------------------------------------------------------

tcm = input.float(0.5, title="Impulse Strength", minval=0.1, maxval=1.0, step = 0.1)

fast_period = input.int(200, "Fast Period EMA")
slow_period = input.int(200, "Slow Period SMA")

fast = ta.ema(close, fast_period)
slow = ta.sma(close, slow_period)

atr = ta.atr(10)

//------------------------------------------------------------------------------
//---------------------------------------------------------------------- Utility
//------------------------------------------------------------------------------

rel_size (val, rel, crit) =>
    val < (crit * rel)

is_fullbody (o, h, l, c, crit) =>
    bull_candle = c > o
    bear_candle = c < o
    candle_range = h - l

    if bull_candle
        rel_size(h - c, candle_range, crit) and rel_size(o - l, candle_range, crit)
    else if bear_candle
        rel_size(c - l, candle_range, crit) and rel_size(h - o, candle_range, crit)
        
//------------------------------------------------------------------------------
//-------------------------------------------------------------- Candle Analysis
//------------------------------------------------------------------------------

significant_candle = ta.tr > (atr + (atr * tcm))

candle_range = (high - low) / 4
high_close = close <= high and (high - candle_range) < close
low_close = close >= low and (low + candle_range) > close
mid_close = (high - candle_range) > close and (low + candle_range) < close

//------------------------------------------------------------------------------
//------------------------------------------------------------------ Trend Logic
//------------------------------------------------------------------------------

bullish_impulse = high_close and significant_candle and is_fullbody(open, high, low, close, 0.2)
bearish_impulse = low_close and significant_candle and is_fullbody(open, high, low, close, 0.2)

//------------------------------------------------------------------------------
//------------------------------------------------------------------------ Plots
//------------------------------------------------------------------------------

var line line_mid = na
var line line_base = na

if bullish_impulse or bearish_impulse
    mid_point = ((high - low) / 2) + low
    candle_clr = bullish_impulse ? color.lime : color.red

    if not na(line_mid)
        line_mid.set_extend(extend.none)
        line_mid.set_x2(bar_index)
    line_mid := line.new(bar_index, mid_point, bar_index + 1, mid_point, extend = extend.right, color = candle_clr, style = line.style_dashed, width = 1)

    if not na(line_base)
        line_base.set_extend(extend.none)
        line_base.set_x2(bar_index)
    line_base := line.new(bar_index, open, bar_index + 1, open, extend = extend.right, color = candle_clr, style = line.style_solid, width = 1)

bullbearclr = close > open ? color.white : color.gray
barclr = bullish_impulse ? color.lime : bearish_impulse ? color.red : bullbearclr
barcolor(barclr)

fast_avg = plot(fast, color = color.new(color.yellow, 100))
slow_avg = plot(slow, color = color.new(color.orange, 100))
fill_clr = fast > slow ? color.new(color.lime, 75) : color.new(color.red, 75)
fill(fast_avg, slow_avg, color = fill_clr)